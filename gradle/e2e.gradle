import java.time.Duration

ext.E2E_DIR = "${project.projectDir}/e2e"

def waitForDockerComposeOutput(String output) {
    while (true) {
        def pb = new ProcessBuilder("docker", "compose", "logs", "-f")
        pb.directory(new File(E2E_DIR))
        def proc = pb.start()
        BufferedReader stdInput = new BufferedReader(new InputStreamReader(proc.getInputStream()))
        String s = null
        while ((s = stdInput.readLine()) != null) {
            println(s)
            if (s.contains(output)) {
                return
            }
        }
        if (proc.exitValue() != 0) {
            throw new RuntimeException("Docker-compose logs retuned non zero exit code")
        }
    }
}

task waitForRunningServer(type: Task, group: "e2e") {
    doLast {
        waitForDockerComposeOutput("Tomcat started on port(s):")
    }
    timeout = Duration.ofSeconds(120)
}

task waitForRunningGatsbyDev(type: Task, group: "e2e") {
    doLast {
        waitForDockerComposeOutput("You can now view tolgee-gatsby-test-app in the browser")
    }
    timeout = Duration.ofSeconds(120)
}

task installE2eDeps(type: Exec, group: "e2e") {
    inputs.file("$E2E_DIR/package.json")
    inputs.file("$E2E_DIR/package-lock.json")
    outputs.dir("$E2E_DIR/node_modules")

    commandLine "npm", "ci"
    workingDir E2E_DIR
}

task downloadExtension(type: Exec, group: "e2e") {
    commandLine "npm", "run", "download-extension"

    workingDir E2E_DIR
}

task runE2e(type: Exec, group: "e2e") {
    dependsOn "buildAll", "runDockerE2e", "installE2eDeps", "downloadExtension"

    commandLine "npm", "run", "cy:run"

    workingDir E2E_DIR

    finalizedBy "stopDockerE2e", "cleanupDockerE2e"
}

task openE2e(type: Exec, group: "e2e") {
    dependsOn "buildAll", "runDockerE2e", "installE2eDeps", "downloadExtension"

    commandLine "npm", "run", "cy:open"

    workingDir E2E_DIR

    finalizedBy "stopDockerE2e", "cleanupDockerE2e"
}

task openE2eDev(type: Exec, group: "e2e") {
    commandLine "npm", "run", "cy:open"
    workingDir E2E_DIR
}

task runDockerE2e(type: Exec, group: "e2e") {
    commandLine "docker", "compose", "up", "-d"
    workingDir E2E_DIR
    finalizedBy "waitForRunningServer"
    finalizedBy "waitForRunningGatsbyDev"
}

task runDockerE2eDevAngular(type: Exec, group: "e2e") {
    commandLine "docker", "compose", "up", "-d", "e2e_app_angular_dev", "e2e_app_angular_prod", "app"
    workingDir E2E_DIR
    finalizedBy "waitForRunningServer"
}

task runDockerE2eDevSvelte(type: Exec, group: "e2e") {
    commandLine "docker", "compose", "up", "-d", "e2e_app_svelte_dev", "e2e_app_svelte_prod", "app"
    workingDir E2E_DIR
    finalizedBy "waitForRunningServer"
}

task stopDockerE2e(type: Exec, group: "e2e") {
    commandLine "docker", "compose", "stop"
    workingDir E2E_DIR
}

task cleanupDockerE2e(type: Exec, group: "e2e") {
    commandLine "docker", "compose", "rm", "-f", "-v"
    workingDir E2E_DIR
}
